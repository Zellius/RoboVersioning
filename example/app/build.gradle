apply plugin: 'com.android.application'
apply plugin: 'com.github.zellius.robo-versioning'

roboVersioning {
    git {
        //configure git
    }
    //quiet()
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "ru.solodovnikov.roboversioning.example"
        minSdkVersion 14
        targetSdkVersion 26
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        roboVersioningFlavor {
            versioningCalculator TAG_DESCRIBE_DIGIT_RC
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            roboVersioningFlavor {
                versioningCalculator TAG_DIGIT
            }
        }
        debug {
            roboVersioningFlavor {
                versioningCalculator TAG_DIGIT_RC
            }
        }
        buildTypeWithoutVersioning {
        }
    }

    flavorDimensions 'dimension'

    productFlavors {
        customVersioning {
            dimension "dimension"
            roboVersioningFlavor {
                versioningCalculator { git ->
                    //you can extract data from git here
                    git.tags()
                    git.execute("describe --tags")
                    ['name': 'custom', 'code': 123]
                }
            }
        }
        customDigitTag {
            versionNameSuffix ".asd"
            dimension "dimension"
            roboVersioningFlavor {
                versioningCalculator customDigitTag {
                    //you can customize standard digit versioning calculator
                    pattern = "(\\d+).(\\d+).(\\d+)"
                    valid = false
                    versionCode = 214
                    versionName = "custom_version_name"
                    emptyVersion = ["name": "empty", "code": 123]
                }
            }
        }
        flavorWithoutVersioning {
            dimension "dimension"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}
